dummyROMHeader: .block
                rts
                .byte 0,0
                rts
                .byte 0,0
                .byte romType6502
                .byte copyright-dummyROMHeader
                .byte $ff
                .text "R"
ram_or_rom_char:
                .text "A"
                .text "M"
copyright:
                .byte 0
                .text "(C)"
size=*-dummyROMHeader
                ; I doubt I've caught all the implied 16s...
                .cerror *-dummyROMHeader!=16

                ; in any event, this ROM header must be <=16 bytes, as
                ; the total pseudo-addressable area has a o documented
                ; size of 4*(16384-16)
                .cerror *-dummyROMHeader>16
                .endblock

dataBankStart=$8000+16
dataBankSize=16384-16

;-------------------------------------------------------------------------
;
; 
initDummyROMHeader:
                jsr selectROMX
                ldy #size(dummyROMHeader)-1
LF881:
                lda dummyROMHeader,y
                sta $8000,y
                dey
                bpl LF881
                bit osfileParameterBlock+1
                bvs LF894       ; taken if called by *SRDATA
                lda #'O'
                sta $8000+(dummyROMHeader.ram_or_rom_char-dummyROMHeader)
LF894:
                stx $8001
                jmp selectTerminalROM

;-------------------------------------------------------------------------
;
; Check if a ROM bank is writeable. 
;
; Entry:
;
; X = ROM bank of interest
;
; Exit:
;
; C=1 if ROM, C=0 if RAM
;
; Terminal ROM selected
;
; Preserves: X
; 
isBankROM:
                jsr selectROMX

                ; Modify the version number byte - if a valid
                ; writeable sideways ROM, don't leave it in a bad
                ; state if reset partway through (not much you can do
                ; for non-ROMs of course...)
                lda $8008
                tay
                eor #$ff
                sei
                sta $8008
                cmp $8008
                sty $8008
                cli
                beq jmpSelectTerminalROM
                clc
jmpSelectTerminalROM:
                jmp selectTerminalROM

;-------------------------------------------------------------------------

